#compdef org-pages

_org-pages () {
  _arguments                                                                        \
    {-h,--help}'[produce help message]'                                             \
    {-d,--debug}'[produce debug logging]'                                           \
    {-v,--verbose}'[produce verbose logging]'                                       \
    {-D,--devel}'[produce devel logging]'                                           \
    '--recursive[export recursively inside directories]'                            \
    '--pdf[export to pdf (through LaTeX)]'                                          \
    '--html[export to html]'                                                        \
    '--generate-home-link[generate a link to the parent index file (only html)]'    \
    '--generate-github-link[generate a link to the github repository (only html)]'  \
    '--generate-org-link[generate a link to the raw org file (only html)]'          \
    '--no-floating-footnote[place footnotes at the bottom of the page (only html)]' \
    '--no-image-conversion[do not convert images from pdf to png (only html)]'      \
    '--keep-tmp-files[keep temporary files]'                                        \
    '--color=[set color scheme]:colors:->colors'                                    \
    '*: :->args'                                                                    \
    && ret=0
  case $state in
    (colors)
      local colors; colors=(blue darkblue green yellow orange red turquoise violet random)
      _describe -t colors 'Color scheme' colors && ret=0
      ;;
    (args)
      local commands; commands=(
        'clean:clean the current directory'
        'generate:generate a local version of the files'
        'publish:publish files to internet'
      )
      _describe -t commands 'command' commands && ret=0
      ;;
  esac
}

_org-pages

# Local Variables:
# mode: Shell-Script
# sh-indentation: 2
# indent-tabs-mode: nil
# sh-basic-offset: 2
# End:
